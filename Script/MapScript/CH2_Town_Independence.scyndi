// License Information:
// **********************************************
// 
// src/Scripts/Script/MapScript/CH2_Town_Independence.scyndi
// (c) Jeroen Broks, 2023, 2024, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 24.01.16
// End License Information
Script

#use "Libs/SCI/Kthura"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"
#use "Libs/SCI/Statistician"

#use "Libs/Music"

#use "Script/Use/Link/Map"
#use "Script/Use/Link/Ach"
#use "Script/Use/Link/Menu"
#use "Script/Use/Link/Party"
#use "Script/Use/Link/Alchemy"
#use "Script/Use/Link/Worldmap"
#use "Script/Use/Link/Textboxes"

#use "Script/Use/General/Pop"
#use "Script/Use/General/Dice"

Global Void ZA_SetCat()
	if !gbool.NewGamePlus;Map.Kill("NPC_Cat");End
	Map.Kill("ZA_SetCat")
End

#region "Scyndi's House"
Global Void ZA_ScyndiHouse()
	If !Done.RachelFoundAndBroughtToScyndiHouse
		MapText("RACHEL1")
	End
	If !Done.ScyndiJoin
		Party.Char(3,"Scyndi")
	End
	Kthura.Obj.Scyndi.Visible=False
	If gbool.Done_SkyTower && !done.Done_SkyTower_Speak_About_Rachel
		Pop.Pop("S","North")
		Pop.Tex("Scyndi","South")
		MapText("RACHEL2")
		WM_Unlocked.GagForest = True
		Awarded.Voltooid_SkyTower = True
		Pop.UnPop()
	End	
	if Party.InParty("Aziella") && !Done.RachelCured
		Pop.Pop("S","North")
		Pop.Tex("Scyndi","South")
		MapText("RACHEL3")
		Pop.UnPop()
		Map.GoToLayer("CITYNIGHT","Myrah")
		Return
	End	
	If gbool.RachelCured && !Done.StoryHarryAfterMath
		plua _Party
		_Party = Statistician.CreateNew("LUNA_PARTY")
		Pop.Pop("S","North")
		Pop.Tex("Scyndi","South")
		MapText("RACHEL4")
		Pop.UnPop()		
		Map.PermaKill("Rachel")				
		Int GLv = 0
		Int SLv = 0
		For cht in each{[0]="Myrah","Francis","Luna","Scyndi","Aziella"}
			plua ch;ch=Party.GetChar(cht)
			GLv = GLv + Ch.Stat.level
			Slv = Slv + Party.SkillLvl(cht,1)
			Slv = Slv + Party.SkillLvl(cht,2)
		End
		Party.NewChar("Rachel",math.max(20,Glv div 4),math.max(15,Glv div 5),math.max(10,Glv div 6))
		Party.Char(5,"Rachel")
		_Party.Char.Rachel.Stat.SKILL_Lvl_1 = math.max(1,SlV div (8+gint.skill))
		_Party.Char.Rachel.Stat.SKILL_Lvl_2 = math.max(1,SlV div (8+gint.skill))
		gstring.CampSituation = "0C_RACHEL"
		Awarded.SCEN_RACHEL=True
	End	
	WM_Unlocked.DarkTunnel = gbool.RachelCured
End

Global Void NPC_Dernor()
	If !gbool.RachelCured
		MapText("DERNOR_RACHEL")
	Else
		MapText("Dernor")
	End
End

Global Void ZA_RosettaNight()
	Music.Push()
	Music.Play("MUSIC/HUB/CRACKLING FIREPLACE.MP3")
	Kthura.Obj.Player.Visible=false
	For y = Kthura.Obj.PreVillage.Y,Kthura.Obj.Campoint.Y
		Cls()
		Map.ScrollX = Kthura.Obj.PreVillage.X
		Map.ScrollY = Y
		Map.DrawScreenNoAuto()
		Flip()
	End
	Kthura.HideByLabel("VP")
	MapText("ROSETTA_MYRAH")
	Awarded.SCEN_HARRY=true
	gbool.STORYHARRY=true
	Music.Pull()
	Map.GoToLayer("SCYNDI","Start")
End
#endregion

#region "Enter Houses"
Global Void NPC_ScyndiHome(); 	Map.GoToLayer("SCYNDI", "Start"); End
Global Void NPC_Shop(); 		Map.GoToLayer("SHOP",   "Start"); End
Global Void NPC_Weapons();		Map.GoToLayer("WEAPONS","Start"); End
#endregion



#region "Regular services"
Global Void NPC_ShopKeeper()
	If Map.Leader=="Scyndi"
		MapText("SHOPKEEPER_SCYNDI")
	Else
		MapText("SHOPKEEPER")
	End
	Menu.Shop()
End

Global Void NPC_Balandar()
	MapText("BALANDAR")
	Menu.Upgrade()
End
#endregion


Global Void NPC_Trixia(); MapText("TRIXIA_"..Map.Leader); End
	
Global Void NPC_Flora()
	If Map.leader=="Francis" && Dice.D20<=Party.SkillLvl("Francis",2) && !Done.HipoRecipeGiven
		MapText("FLORA_HYPO")
		Recipe.Hipo = True
	Else
		MapText("Flora")
	End
End
	
Init
	WM_Unlocked.SkyTower = True
End
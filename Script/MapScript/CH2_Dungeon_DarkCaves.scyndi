// License Information:
// **********************************************
// 
// src/Scripts/Script/MapScript/CH2_Dungeon_DarkCaves.scyndi
// (c) Jeroen Broks, 2024, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 24.01.18
// End License Information
Script

#use "LIBS/SCI/SYS"
#use "LIBS/SCI/KTHURA"
#use "LIBS/SCI/GRAPHICS"
#use "LIBS/SCI/INTERVAR"

#use "LIBS/ROSETTA"

#use "Script/Use/Link/Map"
#use "Script/Use/Link/Ach"
#use "Script/Use/Link/Textboxes"

#use "Script/Use/General/Pop"

Plua Light = {["#000"]=True,["#010"]=True}
Plua ImgDark

Plua GoodSudoku = {}
Plua AnswSudoku = {}

Global Void Dark()
	If !Light[Kthura.CurrentLayer]
		ImgDark = ImgDark || LoadNewImage("GFX/Effects/CircularDarkness.png","CIRCULARDARKNESS")
		ImgDark.Stretch(0,0,Graphics.Width,Graphics.Height)
	End
End


Global Void ZA_StartCheck()
	Map.Kill("ZA_StartCheck")
	If !gbool.DarkCavesDone
		Map.Kill("NPC_GUARD")
		Map.Kill("TRAVELERSEMBLEM_I")
	End
	If !Done.EnteredDarkCavesBeforeOnTheRoadToNeonor
		Map.GoToLayer("#001","Start")
	End
End

Global Void ZA_Dark()
	If !Done.EnteredDarkCavesBeforeOnTheRoadToNeonor_WelcomeText
		MapText("Dark")
	End
End

Global Void Myrah_Remove_Block()
	For i=1,7
		Cls()
		Map.PermaKill(sprintf("BLOCK%d",i))
		Map.DrawScreen()
		Flip()
	End
End

Global Void BreakFree()
	Awarded.SCEN_BreakFree = true
End

Global Void BrugSchakelaar()
	plua Brug = Kthura.Obj.Brug
	Brug.ForcePassible = !Brug.ForcePassible
	Brug.Visible = Brug.ForcePassible
	Kthura.Remap()
End

Global Delegate ZA_21
Global Delegate ZA_31
Global Delegate ZA_41
Global Delegate ZA_12
Global Delegate ZA_32

Global Delegate ZA_13
Global Delegate ZA_23
Global Delegate ZA_43

Global Delegate ZA_14
Global Delegate ZA_24
Global Delegate ZA_34

Global Void Check(String Tag)
	Bool Goed = True
	CSayF("Stepped on Sudoku tile: %s",tag)
	AnswSudoku[Tag] = (AnswSudoku[Tag] % 4) + 1
	Kthura.Obj[Tag].Texture = sprintf("GFX/Combat/Diamonds/FOE_%02d.png",AnswSudoku[Tag])
	For k,v in sPairs(GoodSudoku)
		Goed = Goed && v==AnswSudoku[k]
		CSayF("= Checking %s => Must be: %d and is %d (Good=%s)",k,v,AnswSudoku[k],Goed)
	End
	If Goed
		Map.PermaKill("Deur")
		Awarded.Scen_Sudoku = True
	End
End

Global Void TAR_DEUR()
	Map.PermaKill("Deur")
	Map.PermaKill("Hide")
End

Global Void ZA_WarnBoss()
	Map.PermaKill("ZA_WarnBoss")
	MapText("WARN_"..Map.Leader)
End

Global Void Crack_Boss()
	Map.Boss("Skull")
End

Global Void ZA_Chapter3()
	Map.Chapter(3)
	Map.PermaKill("ZA_Chapter3")
End
Init
	Map.AddCyclePost("Dark")
	
	// Set up puzzle
	GoodSudoku.ZA_21 = 3
	GoodSudoku.ZA_31 = 2
	GoodSudoku.ZA_41 = 4
	GoodSudoku.ZA_12 = 4
	GoodSudoku.ZA_32 = 1
	GoodSudoku.ZA_13 = 3
	GoodSudoku.ZA_23 = 1
	GoodSudoku.ZA_43 = 2
	GoodSudoku.ZA_14 = 2
	GoodSudoku.ZA_24 = 4
	GoodSudoku.ZA_34 = 3
	
	For k,v in Pairs(GoodSudoku)
		CSayF("SudokuDef: %s (Good: %d)",k,v)
		AnswSudoku[k]=0
		Lua.Scyndi.Globals[k] = Lua.load(sprintf("Scyndi.Globals.Check(\"%s\")",k))
	End
End
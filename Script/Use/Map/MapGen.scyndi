// License:
// 	  src/Scripts/Script/Use/Map/MapGen.scyndi
// 
// 	  Version: 24.10.11 VI
// 	
// 	  Copyright (C) 2024 Jeroen Petrus Broks
// 	
// 	  ===========================
// 	  This file is part of a project related to the Phantasar Chronicles or another
// 	  series or saga which is property of Jeroen P. Broks.
// 	  This means that it may contain references to a story-line plus characters
// 	  which are property of Jeroen Broks. These references may only be distributed
// 	  along with an unmodified version of the game.
// 
// 	  As soon as you remove or replace ALL references to the storyline or character
// 	  references, or any termology specifically set up for the Phantasar universe,
// 	  or any other univers a story of Jeroen P. Broks is set up for,
// 	  the restrictions of this file are removed and will automatically become
// 	  zLib licensed (see below).
// 
// 	  Please note that doing so counts as a modification and must be marked as such
// 	  in accordance to the zLib license.
// 	  ===========================
// 
// 
// 	  zLib license terms:
// 	
// 	  This software is provided 'as-is', without any express or implied
// 	  warranty.  In no event will the authors be held liable for any damages
// 	  arising from the use of this software.
// 
// 	  Permission is granted to anyone to use this software for any purpose,
// 	  including commercial applications, and to alter it and redistribute it
// 	  freely, subject to the following restrictions:
// 	
// 	  1. The origin of this software must not be misrepresented; you must not
// 	     claim that you wrote the original software. If you use this software
// 	     in a product, an acknowledgment in the product documentation would be
// 	     appreciated but is not required.
// 	  2. Altered source versions must be plainly marked as such, and must not be
// 	     misrepresented as being the original software.
// 	  3. This notice may not be removed or altered from any source distribution.
// End License

Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/Kthura" 

String LastMap = ""
Table Generated

Int StartX = 32
Int StartY = 128

Int EndX = 3200
int EndY = 3200

Get Int Width;  Return EndX-StartX; End
Get Int Height; Return EndY-StartY; End

Int FloorModuloX = 130
Int FloorModuloY = 130

Int FloorDominance = 3

String FloorTexture = "GFX/TEXTURES/Metal/Metalen Ribbelvloer.jpg"

Int FrontModuloX = 0
Int FrontModuloY = 0
Int FrontDominance = 2
	
String FrontTexture = "GFX/Textures/Hole Textures/Light Yellow.png"

Int GridX = 16
Int GridY = 16

Int MWX = 96
int MWY = 96

int SQX = 160
int SQY = 160

Get int MWHX; return MWX div 2; End
Get int MWHY; return MWY div 2; End
Get int SQHX; return SQX div 2; End
Get int SQHY; return SQY div 2; End

String TransTex = "GFX/Textures/Transporter/Transporter.png"
String TransTag = "TransPic"
Int    TransRad = 32
String TransZAT = "ZA_NextMap" // ZAT = Zone Action Tag (it also means "drunk" in Dutch).

//                    0   1   2   3      4
Var Winds = NewArray("N","E","S","W","SQUARE")

// Square is not really a wind direction, but will create the bigger platforms that surround the start and finish location.

Class MG_Tak
	ReadOnly Int X
	ReadOnly Int Y
	ReadOnly Int W
	ReadOnly Int L
	Get String Wind; Return Winds[W] || "???"; End
	
	// Object X
	Get Int OX
		Switch W
			case 0 2 1
				Return math.max(StartX,x-MWHX)
			case 3
				int r;r=(x-L)-MWHX
				return math.max(r,StartX)
			case 4
				return StartX-SQHX			
		End
		Return 0
	End
	
	// Object Y
	Get Int OY
		Switch W
			case 1 2 3
				Return math.max(StartY,y-MWHY)
			case 0
				int r;r=(x-L)-MWHX
				return math.max(r,StartY)
			case 4
				Return StartY-SQHY
		End
		Return 0
	End

	// Object Width
	Get Int OW
		Switch W
			case 0 2
				Return MWX
			case 1 3
				int r;r = L+MWHX
				if (r+OX>EndX)
					return EndX-OX
				Else
					Return r
				End			
			case 4
				Return SQX
		End
		Return 0
	End
	
	Get Int OH
		Switch W
			case 1 3
				Return MWY
			case 0 2
				int r;r = L+MWHY
				if (r+OY>EndY)
					return EndX-OY
				Else
					Return r
				End	
			case 4
				Return SQY
		End
		Return 0
	End
	
	Get Int OEX; Return OX+OW; End
	Get Int OEY; Return OY+OH; End
	
	Get Int NextX
		Switch W
			case 0 2 3
				return OX+MWHX
			case 1
				Return OEX-MWHX
			case 4
				Return OX
		End
		Return 0
	End
	
	Get Int NextY
		Switch W
			case 0 1 3
				Return OY+MWHX
			case 2
				Return OEY-MWHY
			case 4
				Return OY
		End
	End
	
	Static Int Cnt = 0
	ReadOnly Int ID
	Constructor (Int _X, Int _Y,Int _L,Int _W=10)
		Cnt++; ID=0
		X = (_X div GridX)*GridX // This removes any modulos not being right.
		Y = (_X div GridY)*GridY // This removes any modulos not being right.
		W = _W
		If _W<0 || _W>4; W = math.random(0,3); End
		Switch W
			case 4
				L = 0
			case 0 2
				L = (_L div GridY)*GridY
			default
				L = (_L div GridX)*GridX
		End
		CSayF("Tak #%04d -> P(%d,%d,%d,%d)",ID,_X,_Y,_L,_W)
		CSayF("Tak     -> O(%d,%d,%d,%d) -> %s",OX,OY,OW,OH,Winds[W])
	End
	
	Destructor
		CSay("Tak #%04d has been destroyed")
	End
	
	Bool Generated = False
	Void Generate()
		If Generated; Return; End // Dupe Prevention
		plua platform
		plua bottom
		platform = Kthura.NewTiledArea(OX, OY, OW, OH, FloorTexture)
		platform.ForcePassible = true
		platform.Dominance = FloorDominance
		if FloorModuloX>0;platform.InsertX=-(OX % FloorModuloX); End
		if FloorModuloY>0;platform.InsertY=-(OY % FloorModuloY); End
		bottom = Kthura.NewTiledArea(OX,OY+OH,OW,64,FrontTexture)
		bottom.ForcePassible = true
		bottom.Dominance = FrontDominance
		if FrontModuloX>0;bottom.InsertX=-(OX % FrontModuloX); End
		if FrontModuloY>0;bottom.InsertY=-(OY % FrontModuloY); End		
		Generated = True
	End
	
End

Void Generate()
	// Conflict preventor
	If LastMap!=Kthura.LastLoadedMap
		CSayF("MapGen: New map %s encountered. Resetting generation table",Kthura.LastLoadedMap)
		Generated = {}
		LastMap=Kthura.LastLoadedMap
	End
	If Generated[Kthura.CurrentLayer]
		CSayF("It appears layer '%s' had its map generated before. Getting out!",Kthura.CurrentLayer)
		Return
	End
	plua Start;Start = New MG_Tak(math.random(StartX,EndX),math.random(StartY,EndY),0,4)
	plua StartExit;StartExit = Kthura.NewExit(Start.NextX,Start.NextY,"Start","South")
	Start.Generate()
End


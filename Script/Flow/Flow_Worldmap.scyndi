// License Information:
// src/Scripts/Script/Flow/Flow_Worldmap.scyndi
// Version: 23.10.18
// Copyright (C) 2023 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
Script

#use "Libs/SCI/Sys" 
#use "Libs/SCI/GINIE"
#use "Libs/SCI/Events"
#use "Libs/SCI/Graphics"
#use "Libs/SCI/InterVar"

#use "Libs/MultiPointer"
#use "Libs/GameTime"

#use "Script/Use/General/Loading"

#use "Script/Use/Link/WorldMap"
#use "Script/Use/Link/Map"

Var WMGINIE
Var WMItems
Var WMMeta

Var Ball = ObtainImage("BALL")
Var Fnt  = ObtainFont("BASE")

Init
	WMGINIE = GINIE.Load("Data/General/WorldMap.ini","WORLDMAP")
	WMItems = WMGINIE.Categories
End

var WMIndex(s,key)
	key=Upper(key)
	switch Key
		case "AVAILABLE"
			return Upper(WMGINIE.Value(s["_rec"],"AvailableFromStart"))=="TRUE" || WM_Unlocked[s["_sred"]]
		default
			return WMGINIE.Value(s["_rec"],key)
	end
end

quickmeta WMItem
	index
		WMMeta = WMMeta || {}
		if !WMMeta[key]
			WMMeta[key] = SetMetaTable({["_rec"]=key,["_srec"]=Right(key,len(key)-4)},{["__index"]=WMIndex})
		end
		Return WMMeta[key]
	end
end

Int Hue

Global Void MainFlow()
	int x
	int y
	MultiPointer.Rcv()
	GameTime.Check()
	x,y = 0,0
	For itemrec in each(WMItems)
		plua itemtag
		plua item
		itemtag = Right(itemrec,len(itemrec)-4)
		item = WMItem[itemrec]
		// CSayF("%s> Item Continent: %s; We are on continent: %s => %s", itemrec, item.Continent,gstring.WORLDMAP_LAST, item.Continent==gstring.WORLDMAP_LAST)
		If (item.Continent=="All" || item.Continent==gstring.WORLDMAP_LAST) && (item.Available)
			switch item.type
				case "Camp"
					SetColor(255,180,0)
				case "Town" "City"
					SetColor(0,180,255)
				case "Dungeon"
					if TravelerEmblem[item.Kthura]
						SetColor(0,255,0)
					else
						SetColor(255,0,0)
					end
				default
					SetColor(180,0,255)
			End
			Ball.Draw(x,y)
			SetColor(255,255,255)
			If MX>x && MY>y && MX<x+240 && MY<y+30
				Hue = (Hue+1)%360
				SetColorHSV(Hue,1,1)
				If ML
					CSayF("Right! The user requested to go to %s",Item.Kthura)
					Loading.Show()
					Events.Flush()
					Flip()
					Map.GoToMap(Item.Kthura,Item.Layer,Item.ExitPoint)
					Sys.GoToFlow("FIELD")
					Return
				End
			End
			Fnt.Text(Item.Name,x+40,y)
			y = y + 35
			If y>Graphics.Height-50
				y = 0
				x = x + 250
			End
		End
	End
	MultiPointer.Draw()
End